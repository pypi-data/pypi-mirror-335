# Licensed under the MIT License
# https://github.com/craigahobbs/markdown-up/blob/main/LICENSE


# Include sentinel
if systemGlobalGet('unittestSentinel'):
    return
endif
unittestSentinel = true


# Test statistics
unittestTests = objectNew()
unittestWarnings = arrayNew()


# $function: unittestRunTest
# $group: unittest.mds
# $doc: Run a unit test
# $arg testName: The test function name
function unittestRunTest(testName):
    testFn = unittestRunTestHelper(testName)
    if testFn:
        testFn()
        systemGlobalSet('unittestTestName', null)
    endif
endfunction


# $function: unittestRunTestAsync
# $group: unittest.mds
# $doc: Run an asyncronous unit test
# $arg testName: The test function name
async function unittestRunTestAsync(testName):
    testFn = unittestRunTestHelper(testName)
    if testFn:
        testFn()
        systemGlobalSet('unittestTestName', null)
    endif
endfunction


# unittestRunTest helper function
function unittestRunTestHelper(testName):
    # Single test argument?
    if vTest != null && vTest != testName:
        return null
    endif

    # Test run multiple times?
    if objectHas(unittestTests, testName):
        arrayPush(unittestWarnings, 'Test "' + testName + '" run multiple times')
        return null
    endif

    # Get the test func
    testFn = systemGlobalGet(testName)
    if testFn == null:
        arrayPush(unittestWarnings, 'Test "' + testName + '" not found')
        return null
    endif

    # Add the unit test result array
    testFailures = arrayNew()
    objectSet(unittestTests, testName, testFailures)
    systemGlobalSet('unittestTestName', testName)

    return testFn
endfunction


# $function: unittestReport
# $group: unittest.mds
# $doc: Render the unit test report
# $return: The number of unit test failures
function unittestReport():
    # Compute test statistics
    testNames = arraySort(objectKeys(unittestTests))
    testCount = arrayLength(testNames)
    testFailCount = 0
    for testName in testNames:
        testFailures = objectGet(unittestTests, testName)
        if arrayLength(testFailures):
            testFailCount = testFailCount + 1
        endif
    endfor
    testPassCount = testCount - testFailCount

    # Report statistics
    markdownPrint('', 'Ran ' + testCount + ' tests - ' + testPassCount + ' passed, ' + testFailCount + ' failed')
    if vTest != null:
        testURL = if(vTestArgs != null, '#' + vTestArgs, '#var=')
        markdownPrint('([all tests](' + testURL + '))')
    endif

    # Report any warnings
    testWarningCount = arrayLength(unittestWarnings)
    if testWarningCount:
        markdownPrint('', '## Warnings')
        for warning in unittestWarnings:
            markdownPrint('', '- ' + markdownEscape(warning))
        endfor
    endif

    # Report the failing tests
    if testFailCount:
        markdownPrint('', '## Failing Tests')
        for testName in testNames:
            testFailures = objectGet(unittestTests, testName)
            if arrayLength(testFailures):
                testURL = '#' + if(vTestArgs != null, vTestArgs + '&', '') + "var.vTest='" + urlEncodeComponent(testName) + "'"
                failureLines = arrayNew('', '[' + markdownEscape(testName) + '](' + testURL + ") - FAIL")
                for errorLines in testFailures:
                    for errorLine, ixErrorLine in errorLines:
                        if ixErrorLine == 0:
                            arrayPush(failureLines, '')
                            arrayPush(failureLines, '- ' + errorLine)
                        else:
                            arrayPush(failureLines, '  ' + errorLine)
                        endif
                    endfor
                endfor
                markdownPrint(failureLines)
            endif
        endfor
    endif

    # Report the passing tests
    if testPassCount:
        markdownPrint('', '## Passing Tests')
        for testName in testNames:
            testFailures = objectGet(unittestTests, testName)
            if !arrayLength(testFailures):
                testURL = '#' + if(vTestArgs != null, vTestArgs + '&', '') + "var.vTest='" + urlEncodeComponent(testName) + "'"
                markdownPrint('', '[' + markdownEscape(testName) + '](' + testURL + ") - OK")
            endif
        endfor
    endif

    return testWarningCount + testFailCount
endfunction


# $function: unittestEqual
# $group: unittest.mds
# $doc: Assert an actual value is equal to the expected value
# $arg actual: The actual value
# $arg expected: The expected value
# $arg description: The description of the assertion
function unittestEqual(actual, expected, description):
    if actual != expected:
        unittestRecordError(jsonStringify(actual), jsonStringify(expected), description)
    endif
endfunction


# $function: unittestDeepEqual
# $group: unittest.mds
# $doc: Assert an actual value is *deeply* equal to the expected value
# $arg actual: The actual value
# $arg expected: The expected value
# $arg description: The description of the assertion
function unittestDeepEqual(actual, expected, description):
    actualJSON = jsonStringify(actual)
    expectedJSON = jsonStringify(expected)
    if actualJSON != expectedJSON:
        unittestRecordError(actualJSON, expectedJSON, description)
    endif
endfunction


# Helper function to record a unit test error
function unittestRecordError(actualStr, expectedStr, description):
    testFailures = objectGet(unittestTests, unittestTestName)
    errorLines = arrayNew( \
        '~~~', \
        actualStr, \
        '~~~', \
        '', \
        '!=', \
        '', \
        '~~~', \
        expectedStr, \
        '~~~' \
    )
    if description != null:
        arrayPush(testFailures, arrayExtend(arrayNew(markdownEscape(description), errorLines)))
    else:
        arrayPush(testFailures, errorLines)
    endif
endfunction
